plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    maven {
        url "https://libraries.minecraft.net/"
    }
    maven {
        url "https://repo.cleanroommc.com/releases"
    }
    maven {
        url "https://maven.outlands.top/releases"
    }
    mavenCentral()
}

group = 'top.outlands'
archivesBaseName = 'foundation'
version = '0.12.2'
java.targetCompatibility(JavaVersion.VERSION_21)

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.javassist:javassist:3.30.2-GA'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.0'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'org.burningwave:jvm-driver:8.15.0'
    implementation 'zone.rong:imaginebreaker:2.1'
    implementation 'org.ow2.asm:asm:9.6'
    implementation 'com.cleanroommc:sponge-mixin:0.18.0+mixin.0.8.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

}

compileJava {
    options.compilerArgs += [
            '--add-exports=java.base/jdk.internal.reflect=ALL-UNNAMED',
            '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'
    ]
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.named('test', Test) {
    useJUnitPlatform()

    maxHeapSize = '1G'

    testLogging {
        events "passed"
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "outlands"
            url = "https://maven.outlands.top/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = "foundation"
            version = project.version
            artifact sourcesJar
            from components.java
        }
    }
}